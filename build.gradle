// bintray plugin is used to upload artifacts to a bintray repo
plugins { id "com.jfrog.bintray" version "1.6" }

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = 'org.hildan.utils'
version = '1.1'
description = 'Various classes making CSV files manipulation easier.'

repositories { mavenCentral() }

dependencies { testCompile 'junit:junit:4.+' }

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            // additional jars
            artifact sourcesJar { classifier "sources" }
            artifact javadocJar { classifier "javadoc" }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish = true // the version should be auto published after an upload

    pkg {
        repo = project.hasProperty('bintrayRepoLibs') ? project.bintrayRepoLibs : System.getenv('BINTRAY_REPO_LIBS')
        name = project.name
        desc = project.description
        labels = ['csv', 'reader', 'writer', 'merger']

        String githubUserName = 'joffrey-bion'
        String githubRepoName = project.name
        websiteUrl = 'https://github.com/' + githubUserName + '/' + githubRepoName
        issueTrackerUrl = 'https://github.com/' + githubUserName + '/' + githubRepoName + '/issues'
        vcsUrl = 'https://github.com/' + githubUserName + '/' + githubRepoName + '.git'
        githubRepo = githubUserName + '/' + githubRepoName
        githubReleaseNotesFile = 'README.md'

        licenses = ['MIT']
        version {
            vcsTag = 'v' + project.version
            gpg { sign = true }
        }
    }
}